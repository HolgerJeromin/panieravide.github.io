/* Time domain */

time_domain
  = rule_sequence (any_rule_separator time_domain)*

rule_sequence
  = selector_sequence (" " rule_modifier)?


/* Rule separators */

any_rule_separator
  = normal_rule_separator
  / additionnal_rule_separator
  / fallback_rule_separator

normal_rule_separator
  = "; "

additionnal_rule_separator
  = ", "

fallback_rule_separator
  = " || "


/* Rule modifiers */

rule_modifier
  = "open" (" " comment)?
  / ("closed" / "off") (" " comment)?
  / "unknown" (" " comment)?
  / comment


/* Selectors */

selector_sequence
  = "24/7"
  / wide_range_selectors small_range_selectors

wide_range_selectors
  = month_or_monthday_selector ? week_selector ? ":" ?
  / comment ":"

small_range_selectors
  = weekday_selector ? time_selector ?


/* Time selector */

time_selector
  = timespan ("," timespan)*

timespan
  = time ("-" time)?

time
  = hour_minutes


/* Weekday selector */

weekday_selector
  = weekday_sequence
  / holiday_sequence (("," / " ") weekday_sequence) ?

weekday_sequence
  = weekday_range ("," weekday_range)*

weekday_range
  = wday ("-" wday)?

holiday_sequence
  = holiday ("," holiday)*

holiday
  = singular_day_holiday
  / plural_day_holiday

singular_day_holiday
  = "PH"

plural_day_holiday
  = "SH"


/* Week selector */

week_selector
  = "week " week ("," week)*

week
  = weeknum ("-" weeknum) ?


/* Month selector */

month_or_monthday_selector
  = month_range ("," month_or_monthday_selector)*
  / monthday_range ("," month_or_monthday_selector)*

month_range
  = month ("-" month) ?

monthday_range
  = month ("-" month) ?
  / date_from ("-" date_to) ?

date_from
  = month " " daynum
  / variable_date

date_to
  = date_from
  / daynum

variable_date
  = "easter"


/* Basic elements */

hour
  = [01] [0-9]
  / "2" [01234]

minute
  = [012345] [0-9]

hour_minutes
  = hour ":" minute

wday
  = "Mo" / "Tu" / "We" / "Th" / "Fr" / "Sa" / "Su"

daynum
  = [012] [0-9]
  / "3" [01]

weeknum
  = [01234] [0-9]
  / "5" [123]

month
  = "Jan" / "Feb" / "Mar" / "Apr" / "May" / "Jun" / "Jul" / "Aug" / "Sep" / "Oct" / "Nov" / "Dec"

comment
  = "\"" comment_character+ "\""

comment_character
  = [^\"]